
repositories {
    mavenLocal()
}

// Used to compile classes for Spring Boot 3 that cannot be compiled with a Spring Boot 2 classpath.
// These are all packaged into the same jar and @Conditional annotations are used to load the correct class for
// the spring boot version in use.
sourceSets {
    springBoot3
}

jar {
    from sourceSets.springBoot3.output
}

dependencies {
    // All these dependencies will be provided at runtime by the service this
    // library is injected into.
    compileOnly project(':lib:bootstrapper')
    compileOnly 'org.springframework.security:spring-security-config:5.4.7'
    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
    compileOnly 'org.aspectj:aspectjweaver:1.9.7'
    compileOnly 'org.springframework.boot:spring-boot:2.4.12'
    compileOnly 'org.springframework.boot:spring-boot-autoconfigure:2.4.12'
    compileOnly 'com.github.hmcts:service-auth-provider-java-client:4.0.2'
    compileOnly 'org.springframework.security:spring-security-oauth2-jose:5.4.9'

    springBoot3CompileOnly 'org.springframework.boot:spring-boot:3.2.0'
    springBoot3CompileOnly 'org.springframework.boot:spring-boot-autoconfigure:3.2.0'
    springBoot3CompileOnly group: 'org.springframework', name: 'spring-web', version: '6.1.3'
    springBoot3CompileOnly 'jakarta.servlet:jakarta.servlet-api:6.0.0'

    springBoot3CompileOnly('com.github.hmcts:idam-java-client:2.0.1') {
        transitive = false
    }

    compileOnly('com.github.hmcts:idam-java-client:2.0.1') {
        transitive = false
    }
    compileOnly('com.github.hmcts:auth-checker-lib:2.1.4') {
        transitive = false
    }
    // This is needed at runtime to decode JWTs
    implementation 'com.auth0:java-jwt:3.12.0'

    // Use the fat definition store code at compile time.
    compileOnly 'com.github.hmcts.rse-cft-lib:ccd-definition-store-api:DEV-SNAPSHOT'

    // Provide definition store for running tests
    testImplementation 'com.github.hmcts.rse-cft-lib:ccd-definition-store-api:DEV-SNAPSHOT'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.7.3'
}


test {
    useJUnitPlatform()
    // Test environment variable substitution - this value should be subbed into one of our FixedLists
    environment 'CCD_DEF_CHANGED_NAME_HOW', '"ID": "ChangedNameHow"'
}

// We depend on definition store code which must be published to maven local
compileJava.dependsOn (':publishccd-definition-store-api')


// The cftlib brings in the cftlib Gradle plugin as a composite build.
// This means Gradle attempts to resolve dependency configurations at configuration time instead of execution time,
// which would fail for this project if we haven't yet published definition store to maven local.
// We turn this behaviour off since we don't need dependency substitution
// https://docs.gradle.org/current/userguide/composite_builds.html
configurations.each {
    it.resolutionStrategy.useGlobalDependencySubstitutionRules = false
}

