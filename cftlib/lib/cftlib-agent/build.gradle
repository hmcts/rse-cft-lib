
repositories {
    mavenLocal()
}

dependencies {
    // All these dependencies will be provided at runtime by the service this
    // library is injected into.
    compileOnly project(':lib:bootstrapper')
    compileOnly 'org.springframework.boot:spring-boot:3.4.1'
    compileOnly 'org.springframework.boot:spring-boot-autoconfigure:3.4.1'
    compileOnly group: 'org.springframework', name: 'spring-web', version: '6.1.3'
    compileOnly 'jakarta.servlet:jakarta.servlet-api:6.0.0'
    compileOnly 'org.springframework.security:spring-security-oauth2-jose:6.4.2'
    compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.11.4'
    compileOnly 'commons-io:commons-io:2.11.0'
    compileOnly 'org.aspectj:aspectjweaver:1.9.7'
    compileOnly 'com.github.hmcts:service-auth-provider-java-client:5.3.2'

    compileOnly('com.github.hmcts:idam-java-client:3.0.4') {
        transitive = false
    }
    compileOnly('com.github.hmcts:auth-checker-lib:3.1.1') {
        transitive = false
    }
    // This is needed at runtime to decode JWTs
    implementation 'com.auth0:java-jwt:3.12.0'

    // Use definition store modules at compile time without pulling SB3 transitive deps.
    compileOnly('com.github.hmcts.rse-cft-lib:application:DEV-SNAPSHOT') {
        transitive = false
    }
    compileOnly('com.github.hmcts.rse-cft-lib:excel-importer:DEV-SNAPSHOT') {
        transitive = false
    }

    // Provide definition store parser classes for tests.
    testImplementation('com.github.hmcts.rse-cft-lib:excel-importer:DEV-SNAPSHOT') {
        transitive = false
    }
    testImplementation 'com.google.guava:guava:31.1-jre'
    testImplementation 'org.apache.commons:commons-lang3:3.12.0'
    testImplementation 'org.apache.poi:poi:5.5.1'
    testImplementation 'org.apache.poi:poi-ooxml:5.5.1'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.4.1'
}


test {
    useJUnitPlatform()
    // Test environment variable substitution - this value should be subbed into one of our FixedLists
    environment 'CCD_DEF_CHANGED_NAME_HOW', '"ID": "ChangedNameHow"'
}

// We depend on definition store code which must be published to maven local
compileJava.dependsOn (':publishccd-definition-store-api')

// https://github.com/spring-projects/spring-framework/wiki/Upgrading-to-Spring-Framework-6.x#parameter-name-retention
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add("-parameters")
}

// The cftlib brings in the cftlib Gradle plugin as a composite build.
// This means Gradle attempts to resolve dependency configurations at configuration time instead of execution time,
// which would fail for this project if we haven't yet published definition store to maven local.
// We turn this behaviour off since we don't need dependency substitution
// https://docs.gradle.org/current/userguide/composite_builds.html
configurations.each {
    it.resolutionStrategy.useGlobalDependencySubstitutionRules = false
}
