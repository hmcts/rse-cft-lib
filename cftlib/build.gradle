apply plugin: 'idea'
apply plugin: 'base'

tasks.register('publish')

// Create Gradle tasks that build and publish the CFT submodule projects to maven local.
// We invoke each subproject's build separately using its own Gradle wrapper since each
// project may require a different Gradle version.
project.file('../projects').listFiles().each { dir ->
    if (dir.name != 'ccd-data-store-api') {
        println "importing project ${dir.name}"
        def t = task "publish${dir.name}"(type: Exec) {
            // Enable Gradle's up-to-date checks so we only rebuild the git submodules if they change.
            // Up to date checks require inputs and outputs to be defined, so we use the submodule's git tag as input
            // and the maven local folder of the project as output.
            inputs.property('sha', ['git', 'submodule', 'status', dir].execute().text)
            outputs.dir "${project.repositories.mavenLocal().url}/com/github/hmcts/rse-cft-lib/${dir.name}"
            executable './gradlew'
            args '--no-daemon','-i', '-s', '-I', project.file('init.gradle').path, 'publishToMavenLocal'
            workingDir dir
        }
        publish.dependsOn t
    }
}

publish.dependsOn gradle.includedBuild('rse-cft-lib-plugin').task(':publishToMavenLocal')
publish.dependsOn gradle.includedBuild('dtsse-ccd-config-generator').task(':ccd-gradle-plugin:publishToMavenLocal')

tasks.check.dependsOn gradle.includedBuild('rse-cft-lib-plugin').task(':check')

