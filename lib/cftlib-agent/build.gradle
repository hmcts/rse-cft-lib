
repositories {
    mavenLocal()
}

dependencies {
    // All these dependencies will be provided at runtime by the service this
    // library is injected into.
    compileOnly project(':lib:bootstrapper')
    compileOnly 'org.springframework.security:spring-security-config:5.4.7'
    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
    compileOnly 'org.aspectj:aspectjweaver:1.9.7'
    compileOnly 'org.springframework.boot:spring-boot:2.4.12'
    compileOnly 'org.springframework.boot:spring-boot-autoconfigure:2.4.12'
    compileOnly 'com.github.hmcts:service-auth-provider-java-client:4.0.2'
    compileOnly 'org.springframework.security:spring-security-oauth2-jose:5.4.9'


    compileOnly('com.github.hmcts:idam-java-client:2.0.1') {
        transitive = false
    }
    compileOnly('com.github.hmcts:auth-checker-lib:2.1.4') {
        transitive = false
    }
    // This is needed at runtime to decode JWTs
    implementation 'com.auth0:java-jwt:3.12.0'

    // Intellij cannot resolve classes from the shadow configuration
    // so to fix intellisense we make definition store available as compile only dependencies.
    // https://youtrack.jetbrains.com/issue/IDEA-163411
    rootProject.ext.importedDefStoreModules.each {
        compileOnly ("uk.gov.hmcts.ccd.definition:${it}") {
            transitive = false
        }
    }
    // Use the fat definition store code at compile time.
    compileOnly (project (path: ':projects:definition-store-fat', configuration: 'shadow'))

    // Provide definition store for running tests
    testImplementation 'com.github.hmcts.rse-cft-lib:definition-store-fat:DEV-SNAPSHOT'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.7.3'
}


test {
    useJUnitPlatform()
    // Test environment variable substitution - this value should be subbed into one of our FixedLists
    environment 'CCD_DEF_CHANGED_NAME_HOW', '"ID": "ChangedNameHow"'
}
