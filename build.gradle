apply plugin: 'idea'
apply plugin: 'base'

task publish

// Create Gradle tasks that build and publish the CFT submodule projects to maven local.
// We invoke each subproject's build separately using its own Gradle wrapper since each
// project may require a different Gradle version.
project.file('projects').listFiles().each { dir ->
    if (!(dir.name =~ /definition-store/)) {
        def t = task "publish${dir.name}"(type: Exec) {
            workingDir dir
            commandLine './gradlew', '-i', '-I', project.file('init.gradle').path, 'publishToMavenLocal'
            doFirst {
                // Gradle requires this in nested builds.
                new File(dir, 'settings.gradle').createNewFile()
            }
        }
        publish.dependsOn t
    }
}

publish.dependsOn ':projects:definition-store-fat:doPublish'

tasks.check.dependsOn gradle.includedBuild('rse-cft-lib-plugin').task(':check')

task publishSecrets(type: Exec) {
  commandLine 'az', 'keyvault', 'secret', 'set', "--vault-name", "rse-cft-lib", "--name", "aat-env", "--file", "test-project/build/cftlib/.aat-env"
}
