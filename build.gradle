apply plugin: 'idea'
apply plugin: 'base'

task publish
gradle.includedBuilds.each {
    if (it.name != 'ccd-definition-store-api') {
        publish.dependsOn it.task(':publishToMavenLocal')
    }
}

// TODO: rework all included projects away from composite builds.
task publishAM(type: Exec) {
    workingDir project.file('projects/am-role-assignment-service').path
    commandLine './gradlew', '-i', '-I', project.file('init.gradle').path, 'publishToMavenLocal'
    doFirst {
        // Gradle requires this in nested builds.
        project.file('projects/am-role-assignment-service/settings.gradle').createNewFile()
    }
}

publish.dependsOn ':projects:definition-store-fat:doPublish', publishAM

tasks.check.dependsOn gradle.includedBuild('rse-cft-lib-plugin').task(':check')

task publishSecrets(type: Exec) {
  commandLine 'az', 'keyvault', 'secret', 'set', "--vault-name", "rse-cft-lib", "--name", "aat-env", "--file", "test-project/build/cftlib/.aat-env"
}
