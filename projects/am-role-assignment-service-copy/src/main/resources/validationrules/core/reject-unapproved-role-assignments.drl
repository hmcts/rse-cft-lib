package validationrules.core;
import uk.gov.hmcts.reform.roleassignment.domain.model.RoleAssignment;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.Status;
import function uk.gov.hmcts.reform.roleassignment.domain.service.common.ValidationModelService.logMsg;

/*
 * Explicitly reject any role assignments to be created which have not been approved.
 * Salience = -1000 to ensure this is run last, after all rules which could validate
 * the role assignment have had a chance to fire.
 */
rule "reject_unapproved_create_role_assignments"
salience -1000
when
    $ra: RoleAssignment(status in ( Status.CREATE_REQUESTED, Status.CREATE_APPROVED))
then
    logMsg("Rule : reject_unapproved_create_role_assignments");
    $ra.setStatus(Status.REJECTED);
    $ra.log("Create not approved by any rule, hence rejected  : reject_unapproved_create_role_assignments");
    update($ra);
end;

/*
 * Explicitly reject any role assignments to be deleted which have not been approved.
 * Salience = -1000 to ensure this is run last, after all rules which could validate
 * the role assignment have had a chance to fire.
 */
rule "reject_unapproved_delete_role_assignments"
salience -1000
when
    $ra: RoleAssignment(status == Status.DELETE_REQUESTED)
then
    logMsg("Rule : reject_unapproved_delete_role_assignments");
    $ra.setStatus(Status.DELETE_REJECTED);
    $ra.log("Delete not approved by any rule, hence ejected  : reject_unapproved_delete_role_assignments");
    update($ra);
end;
