package validationrules.publiclaw;
import uk.gov.hmcts.reform.roleassignment.domain.model.Assignment;
import uk.gov.hmcts.reform.roleassignment.domain.model.RoleAssignment;
import uk.gov.hmcts.reform.roleassignment.domain.model.Request;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.Classification
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.Status;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.RequestType;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.GrantType;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.RoleCategory;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.RoleType
import uk.gov.hmcts.reform.roleassignment.domain.model.ExistingRoleAssignment;
import uk.gov.hmcts.reform.roleassignment.domain.model.Case;
import function uk.gov.hmcts.reform.roleassignment.domain.service.common.ValidationModelService.logMsg;
import uk.gov.hmcts.reform.roleassignment.domain.model.FeatureFlag;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.FeatureFlagEnum;
import uk.gov.hmcts.reform.roleassignment.util.ValidationUtil;
import java.time.ZonedDateTime;
import java.time.temporal.ChronoUnit;
import uk.gov.hmcts.reform.roleassignment.domain.model.CaseAllocatorApproval;



/*
 * PUBLICLAW "allocated-judge" case validation rule.
 */
rule "publiclaw_allocated_judge_create_case_roles"
when
    $ca: CaseAllocatorApproval(
                 roleAssignment.status == Status.CREATE_REQUESTED,
                 roleAssignment.attributes["jurisdiction"].asText() == "PUBLICLAW",
                 roleAssignment.attributes["caseType"].asText() == "CARE_SUPERVISION_EPO",
                 roleAssignment.roleName in ("allocated-judge"))
         ExistingRoleAssignment(
                 actorId == $ca.getRoleAssignment().actorId,
                 roleType == RoleType.ORGANISATION,
                 roleName in ("judge", "fee-paid-judge"),
                 attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() == "PUBLICLAW")
then
    $ca.getRoleAssignment().setStatus(Status.CREATE_APPROVED);
    $ca.getRoleAssignment().log("Stage 1 approved : publiclaw_allocated_judge_create_case_roles");
    update($ca.getRoleAssignment());
    logMsg("Rule : publiclaw_allocated_judge_create_case_roles");
end;

/*
 * PUBLICLAW "allocated-magistrate" case validation rule.
 */
rule "publiclaw_allocated_magistrate_create_case_roles"
when
    $ca: CaseAllocatorApproval(
                 roleAssignment.status == Status.CREATE_REQUESTED,
                 roleAssignment.attributes["jurisdiction"].asText() == "PUBLICLAW",
                 roleAssignment.attributes["caseType"].asText() == "CARE_SUPERVISION_EPO",
                 roleAssignment.roleName in ("allocated-magistrate"))
         ExistingRoleAssignment(
                 actorId == $ca.getRoleAssignment().actorId,
                 roleType == RoleType.ORGANISATION,
                 roleName in ("magistrate"),
                 attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() == "PUBLICLAW")
then
    $ca.getRoleAssignment().setStatus(Status.CREATE_APPROVED);
    $ca.getRoleAssignment().log("Stage 1 approved : publiclaw_allocated_magistrate_create_case_roles");
    update($ca.getRoleAssignment());
    logMsg("Rule : publiclaw_allocated_magistrate_create_case_roles");
end;

/*
 * PUBLICLAW "hearing-judge" case validation rule.
 */
rule "publiclaw_hearing_judge_create_case_roles"
when
    $ca: CaseAllocatorApproval(
                 roleAssignment.status == Status.CREATE_REQUESTED,
                 roleAssignment.attributes["jurisdiction"].asText() == "PUBLICLAW",
                 roleAssignment.attributes["caseType"].asText() == "CARE_SUPERVISION_EPO",
                 roleAssignment.roleName in ("hearing-judge"))
         ExistingRoleAssignment(
                 actorId == $ca.getRoleAssignment().actorId,
                 roleType == RoleType.ORGANISATION,
                 roleName in ("judge", "fee-paid-judge"),
                 attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() == "PUBLICLAW")
then
    $ca.getRoleAssignment().setStatus(Status.CREATE_APPROVED);
    $ca.getRoleAssignment().log("Stage 1 approved : publiclaw_hearing_judge_create_case_roles");
    update($ca.getRoleAssignment());
    logMsg("Rule : publiclaw_hearing_judge_create_case_roles");
end;

/*
 * The case roles can be deleted if CaseAllocatorApproval entity is available for requested role assignment.
 */
rule "publiclaw_staff_case_allocator_approved_delete_case_role"
when
    $ca: CaseAllocatorApproval(
                roleAssignment.status == Status.DELETE_REQUESTED,
                roleAssignment.attributes["jurisdiction"].asText() == "PUBLICLAW")
then
    $ca.getRoleAssignment().setStatus(Status.DELETE_APPROVED);
    $ca.getRoleAssignment().log("Stage 1 approved : publiclaw_staff_case_allocator_approved_delete_case_role");
    update($ca.getRoleAssignment());
    logMsg("Rule : publiclaw_staff_case_allocator_approved_delete_case_role");
end;
