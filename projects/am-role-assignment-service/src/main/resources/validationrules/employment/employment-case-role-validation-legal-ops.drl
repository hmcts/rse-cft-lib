package validationrules.employment;
import uk.gov.hmcts.reform.roleassignment.domain.model.Assignment;
import uk.gov.hmcts.reform.roleassignment.domain.model.RoleAssignment;
import uk.gov.hmcts.reform.roleassignment.domain.model.Request;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.Status;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.RequestType;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.GrantType;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.RoleCategory;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.RoleType
import uk.gov.hmcts.reform.roleassignment.domain.model.ExistingRoleAssignment;
import uk.gov.hmcts.reform.roleassignment.domain.model.CaseAllocatorApproval;
import uk.gov.hmcts.reform.roleassignment.domain.model.Case;
import function uk.gov.hmcts.reform.roleassignment.domain.service.common.ValidationModelService.logMsg;
import uk.gov.hmcts.reform.roleassignment.domain.model.FeatureFlag;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.FeatureFlagEnum;
import uk.gov.hmcts.reform.roleassignment.util.ValidationUtil;

import java.time.ZonedDateTime;
import java.time.ZoneOffset
import java.time.temporal.ChronoUnit;

/*
 * The "allocated-tribunal-caseworker" case role
 */
rule "employment_allocated_tribunal_caseworker_create_case_roles"
when
    $ca: CaseAllocatorApproval(
                 roleAssignment.status == Status.CREATE_REQUESTED,
                 roleAssignment.attributes["jurisdiction"].asText() == "EMPLOYMENT",
                 roleAssignment.roleName == "allocated-tribunal-caseworker")
         ExistingRoleAssignment(
                 actorId == $ca.getRoleAssignment().actorId,
                 roleType == RoleType.ORGANISATION,
                 roleName in ("tribunal-caseworker", "senior-tribunal-caseworker"),
                 attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() == "EMPLOYMENT")
then
    $ca.getRoleAssignment().setStatus(Status.CREATE_APPROVED);
    $ca.getRoleAssignment().log("Stage 1 approved : employment_allocated_tribunal_caseworker_create_case_roles");
    update($ca.getRoleAssignment());
    logMsg("Rule : employment_allocated_tribunal_caseworker_create_case_roles");
end;

/*
 * The "allocated-admin-caseworker" case role
 */
rule "employment_allocated_admin_caseworker_create_case_roles"
when
    $ca: CaseAllocatorApproval(
                 roleAssignment.status == Status.CREATE_REQUESTED,
                 roleAssignment.attributes["jurisdiction"].asText() == "EMPLOYMENT",
                 roleAssignment.roleName == "allocated-admin-caseworker")
         ExistingRoleAssignment(
                 actorId == $ca.getRoleAssignment().actorId,
                 roleType == RoleType.ORGANISATION,
                 roleName in ("hearing-centre-admin", "hearing-centre-team-leader", "regional-centre-admin",
                 "regional-centre-team-leader", "clerk"),
                 attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() == "EMPLOYMENT")
then
    $ca.getRoleAssignment().setStatus(Status.CREATE_APPROVED);
    $ca.getRoleAssignment().log("Stage 1 approved : employment_allocated_admin_caseworker_create_case_roles");
    update($ca.getRoleAssignment());
    logMsg("Rule : employment_allocated_admin_caseworker_create_case_roles");
end;

/*
 * The "allocated-ctsc-caseworker" case role
 */
rule "employment_allocated_ctsc_caseworker_create_case_roles"
when
    $ca: CaseAllocatorApproval(
                 roleAssignment.status == Status.CREATE_REQUESTED,
                 roleAssignment.attributes["jurisdiction"].asText() == "EMPLOYMENT",
                 roleAssignment.roleName == "allocated-ctsc-caseworker")
         ExistingRoleAssignment(
                 actorId == $ca.getRoleAssignment().actorId,
                 roleType == RoleType.ORGANISATION,
                 roleName in ("ctsc", "ctsc-team-leader"),
                 attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() == "EMPLOYMENT")
then
    $ca.getRoleAssignment().setStatus(Status.CREATE_APPROVED);
    $ca.getRoleAssignment().log("Stage 1 approved : employment_allocated_ctsc_caseworker_create_case_roles");
    update($ca.getRoleAssignment());
    logMsg("Rule : employment_allocated_ctsc_caseworker_create_case_roles");
end;
